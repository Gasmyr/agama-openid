// Runs the auth code flow and retrieves user profile data
Flow org.gluu.inbound.oauth2.AuthzCodeWithUserInfo
     Basepath ""
     Inputs oauthParams
// Launches the code flow
obj = Trigger org.gluu.inbound.oauth2.AuthzCode oauthParams
token = obj.data.access_token
// Calls the userinfo endpoint and parses the request as a map
p | E = Call org.gluu.util.NetworkUtils#mapFromGetRequestWithToken oauthParams.userInfoEndpoint token
When E is not null
     Log "@error " E
     msg = [ "Unable to retrieve user profile.", E.message ]
     msg = Call java.lang.String#join " " msg
     obj = { success: false, error: msg }
     Finish obj
Log "@debug Profile data\n" p
obj = { success: true, data: { profile: p, tokenResponse: obj.data } }
Finish obj